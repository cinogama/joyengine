// 这个倒霉文件用于管理项目中使用的C++模块，负责指导模块元数据读取载入编辑等

import woo.std;
import je.filesys;
import je.gui;

import platform;

using je::gui;

using CxxProjectBuildConfig = struct {
    // 配置名称
    config: string,

    // 生成的cmake内容
    cmake: string,

    // 当前配置编译前，CMake需要的命令行参数
    cmake_option: string,

    // 当前配置编译时执行的命令
    built_command: string,
};

public using CxxProject = struct {
    name: string, // 模块的名称
    path: string, // 模块所在路径，仅此而已
    configs: dict<string, CxxProjectBuildConfig>, // 可用的生成配置
    mut module_instance: option<handle>,
}
{
    // Cmake所在路径，项目会使用此路径指示的cmake
    let CMAKE_PATH = F"{std::exepath()}builtin/cmake/win32/bin/cmake.exe"; 
        // F"{je::filesys::env("CMAKE")}/cmake.exe";
    let MSVC_PATH = F"{je::filesys::env("MSBUILD")}/MSBuild.exe";

    // C++模块样例，创建项目文件时会复制这个目录
    let CXX_PROJECT_EXAMPLE_PATH = F"{std::exepath()}builtin/cxxproject/";

    // 引擎的头文件路径
    public let ENGINE_HEADER_PATH = F"{std::exepath()}builtin/include/";

    // 引擎的库目录
    let ENGINE_CORE_LIBRARYS_LOCATE_PATH = std::exepath();
    let ENGINE_CORE_LIBRARYS_PATHS =
        {
            ["win32_debug_x86"] = [
                F"libwoo32_debug.dll",
                F"libwoo32_debug.lib",
                F"libjoyecs32_debug.dll",
                F"libjoyecs32_debug.lib",
            ],
            ["win32_release_x86"] = [
                F"libwoo32.dll",
                F"libwoo32.lib",
                F"libjoyecs32.dll",
                F"libjoyecs32.lib",
            ],
            ["win32_debug_x64"] = [
                F"libwoo_debug.dll",
                F"libwoo_debug.lib",
                F"libjoyecs_debug.dll",
                F"libjoyecs_debug.lib",
            ],
            ["win32_release_x64"] = [
                F"libwoo.dll",
                F"libwoo.lib",
                F"libjoyecs.dll",
                F"libjoyecs.lib",
            ],
        }: dict<string, array<string>>;

    alias platform_t = string;

    public func update_engine_file(cxx_proj_path: string)
    {
        using je::file;
        using je::filesys;

        // 从引擎根目录更新引擎相关头文件和库文件
        if (!exist(cxx_proj_path))
            return result::err(F"无法找到C++模块项目，{cxx_proj_path}目录不存在");

        // 1. 删除engine目录
        je::filesys::delete(F"{cxx_proj_path}/engine");
        je::filesys::mkdir(F"{cxx_proj_path}/engine");
        je::filesys::mkdir(F"{cxx_proj_path}/engine/library");

        // 2. 把新的engine移过来，把引擎的头文件复制过来
        let r = copy(ENGINE_HEADER_PATH, F"{cxx_proj_path}engine/include");

        if (r->iserr)
            return r->fail;

        // 2. 开始复制库文件, 顺便确认支持的平台
        let platform_support = {}mut : map<platform_t, bool>;
        for (let platform, files : ENGINE_CORE_LIBRARYS_PATHS)
        {
            files
                ->map(\name: string = copy(F"{ENGINE_CORE_LIBRARYS_LOCATE_PATH}{name}", F"{cxx_proj_path}engine/library/{name}");)
                ->map(\r: result<anything, anything> = r->isok;)
                ->reduce(\l: bool, r: bool = l ? r | l;)
                ->map(\r: bool = platform_support->set(platform, r););
        }

        return result::ok(platform_support);
    }

    public func generate_module_config(name: string, cxx_proj_path: string, platform_support: map<platform_t, bool>)
    {      
        using je::file;
        using je::filesys;
        
        let enabled_generating = platform_support
            ->forall(\_: anything, enable: bool = enable;)
            ->unmapping
            ->> \conf_enab: (string, anything) = conf_enab[0];
            ;
            
        let generating_results = enabled_generating
            ->> \conf: string = (conf, generate_cmake_doc(conf));
            ;

        let failed_generating = generating_results
            ->forall(\r: (anything, result<anything, anything>) = r[1]->iserr;);

        if (!failed_generating->empty)
        {
            for (let _, r : failed_generating ->> \r = r[1]->error->val;)
                je::logerr(r);

            return result::err(F"创建模块的CMake文件时失败：\n{
                failed_generating
                    ->> \r = r[1]->error->val;
                    ->reduce(\l, r = l + "\n" + r;)
                    ->val
            }");
        }

         // 3. 给支持的平台生成CMake文件，并且把配置信息存档
        writeall(F"{cxx_proj_path}config.inf", {
            ["name"] = name: dynamic,
            ["path"] = normalize(cxx_proj_path): dynamic,
            ["conf"] = enabled_generating: dynamic,
        }: string);
        
        for (let _, config : generating_results ->> \c = c[1]->okay->val;)
        {
            // 3.1 把生成出来的CMake保存起来
            let aim_cmake_dir = F"{cxx_proj_path}builder/{config.config}";
            if (!exist(aim_cmake_dir))
            {
                // 对应目录不存在，在这里创建一下，失败就寄掉
                let mkdir_result = mkdir(aim_cmake_dir);
                if (mkdir_result->iserr)
                    return mkdir_result->fail;
            }
            if (!writeall(F"{aim_cmake_dir}/CMakeLists.txt", 
                config.cmake
                    ->replace("$$JEMODULE_PROJECT_NAME$$", name)
                    ->replace("$$JEMODULE_BUILD_CONFIG$$", config.config)))
                return result::err(F"无法写入'{aim_cmake_dir}/CMakeLists.txt'");
        }

        // 4. 生成主CMakeLists
        let cmake_head = 
@"# C++ Module for JoyECS
cmake_minimum_required (VERSION 3.8)

project ("$$JEMODULE_PROJECT_NAME$$")
set(CMAKE_CXX_STANDARD 17)

if(MSVC) 
    add_compile_options("$<$<CXX_COMPILER_ID:MSVC>:/source-charset:utf-8>")
    add_compile_options("$<$<C_COMPILER_ID:MSVC>:/source-charset:utf-8>")
endif()

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/build/${JEMODULE_BUILT_AIM}")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/build/${JEMODULE_BUILT_AIM}")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/build/${JEMODULE_BUILT_AIM}")
set(LIBRARY_OUTPUT_PATH "${CMAKE_CURRENT_SOURCE_DIR}/build")
"@;
        let cmake_subdirs = enabled_generating
            ->map(func(conf: string){
                let check_if_need_build = F"if ($\{JEMODULE_BUILT_AIM\} STREQUAL \"{conf}\")";
                let include_subdir = F"    add_subdirectory(\"builder/{conf}\")";
                return F"{check_if_need_build}\n{include_subdir}";
            })
            ->reduce(\l: string, r: string = F"{l}\nelse{r}";)
            ->map(\s: string = F"{s}\nelse()\n    message(FATAL_ERROR \"Unknown built aim.\")\nendif()";);

        if (!cmake_subdirs->has)
        {
            std::panic("没有可用的配置，这绝不应该发生");
            return result::err("没有可用的配置，请检查引擎目录相关文件是否丢失");
        }

        if (!writeall(F"{cxx_proj_path}CMakeLists.txt", 
                F"{cmake_head}\n{cmake_subdirs->val}\n"
                    ->replace("$$JEMODULE_PROJECT_NAME$$", name)))
            return result::err(F"无法写入'{cxx_proj_path}CMakeLists.txt'");

        return result::ok(generating_results);
    }

    public func start(name: string, locpath: string)=> result<CxxProject, string>
    {
        using je::file;
        using je::filesys;

        // 在指定目录下创建Cxx项目目录
        let cxx_proj_path = F"{normalize(locpath)}{name}.jemodule4/";
        if (exist(cxx_proj_path))
            return result::err(F"无法创建C++模块项目，{cxx_proj_path}目录已经存在");

        let r = copy(CXX_PROJECT_EXAMPLE_PATH, cxx_proj_path)
            =>> \_ = update_engine_file(cxx_proj_path);;
        if (r->iserr)
        {
            delete(cxx_proj_path);
            return r->fail;
        }
        let platform_support = r->okay->val;

        match (generate_module_config(name, cxx_proj_path, platform_support))
        {
        err(reason)?
        {
            delete(cxx_proj_path);
            return result::err(reason);
        }
        ok(generating_results)?
        {       
            let created_module = CxxProject{ 
                name = name, 
                path = normalize(cxx_proj_path), 
                configs = generating_results
                    ->mapping
                    ->map(\k: string, v: result<CxxProjectBuildConfig, string> 
                        = (k, v->okay->val);),
                module_instance = option::none,
                };

            // 最后，编译一下当前平台
            created_module->update_compile(je::editor::platform());

            return result::ok(created_module);
        }
        }
    }

    func search_file_in(dir: string, exname: string)=> option<string>
    {
        using je::filesys;

        for (let _, p : childs(dir)->okay->val)
        {
            if (isdir(p))
            {
                let r = search_file_in(p, exname);
                if (r->has)
                    return r;
            }
            else if (externname(p) == exname)
                return option::value(p);
        }
        return option::none;
    }
    func load_library_of_platform(self: CxxProject, config: string)
    {
        assert(!self.module_instance->has);
        match (search_file_in(get_current_runtime_path(self->get_runtime_path(config))->val, "dll"))
        {
        value(p)? 
            return self.module_instance = je::editor::load_module(self.name, p);
        none? 
            return self.module_instance = option::none;
        }
    }
    func get_current_runtime_id(runtime_dll_path: string)
    {
        using je::filesys;

        return 
            childs(runtime_dll_path)
                ->okay
                =>> \child_path = child_path
                    ->  forall(\path = isdir(path);)
                    ->> \path = filename(path): int;
                    ->  reduce(\l, r = l > r ? l | r;);
                ;
    }
    func get_current_runtime_path(runtime_dll_path: string)
    {
        using je::filesys;
        return 
            get_current_runtime_id(runtime_dll_path)
                ->map(\id: int = normalize(F"{normalize(runtime_dll_path)}{id}"););
    }
    func get_newest_runtime_path(runtime_dll_path: string)
    {
        using je::filesys;
        let newest_path = 
            get_current_runtime_id(runtime_dll_path)
                ->map(\id: int = id + 1;)
                ->valor(0)
                ->\id: int = F"{normalize(runtime_dll_path)}{id}";
                ;
        assert(!exist(newest_path));
        mkdir(newest_path);
        assert(exist(newest_path));

        return normalize(newest_path);
    }
    func get_runtime_path(self: CxxProject, config: string)
    {
        return F"{self.path}runtime/{config}/";
    }
    func update_library_of_platform(self: CxxProject, config: string)
    {
        using je::filesys;

        // 获取编译出来的二进制，移动到一个统一的位置
        if (!exist(self->get_runtime_path(config)))
            mkdir(self->get_runtime_path(config));

        assert(exist(self->get_runtime_path(config)));

        let newest_runtime_path = get_newest_runtime_path(self->get_runtime_path(config));

        match (search_file_in(F"{self.path}build/{config}", "dll"))
        {
        value(p)? 
            assert(copy(p, F"{newest_runtime_path}{filename(p)}")->isok);
        none? std::panic("Output binary file not found;");
        }

        // 调试时，vs调试器可能会自动附加pdb，导致重新编译失败
        // 为了避免蠢蛋vs干蠢事，这里直接把pdb文件移到runtime目录下
        match (search_file_in(F"{self.path}build/{config}", "pdb"))
        {
        value(p)?
            assert(move(p, F"{newest_runtime_path}{filename(p)}")->isok);
        none?
            ; // 没有pdb就算了，可能是release
        }
    }

    public func open_for_loading(path: string)
    {
        using je::filesys;
        using je::file;

        // 加载指定路径的 CXX 项目信息
        if (!exist(path))
            return result::err(F"找不到模块{path}，请检查文件是否丢失");
        if (!exist(F"{normalize(path)}config.inf"))
            return result::err(F"找不到配置文件{normalize(path)}config.inf，请检查文件是否丢失");

        let readdat = readall(F"{normalize(path)}config.inf")
            =>> \d = d->todict;;
        if (!readdat->has)
            return result::err(F"模块{path}的配置文件无法读取或格式不正确");

        let created_module = CxxProject{ 
            name = readdat->val["name": dynamic] as string, 
            path = normalize(readdat->val["path": dynamic] as string), 
            configs = readdat
                ->val["conf": dynamic] as array
                ->map(\d: dynamic = d as string;)
                // 如果文件打不开，直接在这里爆炸
                ->map(\conf: string = (conf, generate_cmake_doc(conf)->okay->val);)
                ->mapping,
            module_instance = option::none,
            };
        
        let current_je_proj = editor::Project::GetCurrentProject();
        current_je_proj.native_modules->getorset(created_module.path, created_module);

        return result::ok(created_module);
    }

    public func load(path: string)
    {
        using je::filesys;
        using je::file;

        match(open_for_loading(path))
        {
        err(errmsg)?
            return result::err(errmsg);
        ok(created_module)?
        {
            let config = je::editor::platform();

            // 获取最新的模块版本，将其保留并重命名为0，删除其他全部版本
            get_current_runtime_id(created_module->get_runtime_path(config))
                ->map(\newest_id: int = 
                    childs(created_module->get_runtime_path(config))
                        ->map(\child_paths: array<string> =
                            child_paths
                                ->forall(\path: string = !isdir(path) || newest_id != filename(path): int;)
                                ->map(\path: string = delete(path);)
                        ;)
                ;)
            ;

            let founding_newest_result = 
                get_current_runtime_path(created_module->get_runtime_path(config))
                    ->map(\dllpath: string = 
                        filename(dllpath) == "0" 
                        ? result::ok("无需清理旧的模块")
                        | move(dllpath, F"{created_module->get_runtime_path(config)}0");)
                    ->or(\ = result::err("清理模块失败，找不到最新的模块");)
                    ->val;
            if (founding_newest_result->iserr)
                return founding_newest_result->fail;

            if (!created_module->load_library_of_platform(config)->has)
                return result::err(F"无法为当前运行时{config}加载模块，请检查");

            return result::ok(created_module);
        }
        
        }// End of match
    }

    func generate_cmake_doc(conf: string)=> result<CxxProjectBuildConfig, string>
    {
        let win32_cmake = @"
# Build config for $$JEMODULE_PROJECT_NAME$$
cmake_minimum_required (VERSION 3.8)

include_directories("../../engine/include")
include_directories("../../systems")
include_directories("../../components")

find_library(joyengine_lib "$$JE_ENGINE_LIB$$" ../../engine/library)
find_library(woolang_lib "$$JE_WOOLANG_LIB$$" ../../engine/library)

file(GLOB_RECURSE module_systems_sources ../../systems/*.cpp)
file(GLOB_RECURSE module_systems_headers ../../systems/*.hpp)
file(GLOB_RECURSE module_components_headers ../../components/*.hpp)        

add_library($$JEMODULE_PROJECT_NAME$$ SHARED 
    ${module_systems_sources}
    ${module_systems_headers}
    ${module_components_headers}
    ../../main.cpp
    ../../loader.hpp)

set_target_properties($$JEMODULE_PROJECT_NAME$$ PROPERTIES OUTPUT_NAME lib$$JEMODULE_PROJECT_NAME$$)
set_target_properties($$JEMODULE_PROJECT_NAME$$ PROPERTIES PREFIX "")

set_target_properties($$JEMODULE_PROJECT_NAME$$ PROPERTIES RELEASE_POSTFIX "$$JE_POSTFIX$$")
set_target_properties($$JEMODULE_PROJECT_NAME$$ PROPERTIES DEBUG_POSTFIX "$$JE_POSTFIX$$")

target_link_libraries($$JEMODULE_PROJECT_NAME$$ ${joyengine_lib} ${woolang_lib})
        "@;
        
        // 根据给定的配置和项目路径等信息，生成CMake文档
        let static config_cmake_file = {
            ["win32_debug_x86"] = CxxProjectBuildConfig{ 
                config = "win32_debug_x86",
                cmake = win32_cmake
                    ->replace("$$JE_ENGINE_LIB$$",  "libjoyecs32_debug.lib")
                    ->replace("$$JE_WOOLANG_LIB$$", "libwoo32_debug.lib")
                    ->replace("$$JE_POSTFIX$$", "32_debug"),
                cmake_option = "-G \"Visual Studio 16 2019\" -A x86 -DCMAKE_BUILD_TYPE=DEBUG",
                built_command = "-p:Configuration=Debug",
            },
            ["win32_release_x86"] = CxxProjectBuildConfig{ 
                config = "win32_release_x86",
                cmake = win32_cmake
                    ->replace("$$JE_ENGINE_LIB$$",  "libjoyecs32.lib")
                    ->replace("$$JE_WOOLANG_LIB$$", "libwoo32.lib")
                    ->replace("$$JE_POSTFIX$$", "32"),
                cmake_option = "-G \"Visual Studio 16 2019\" -A x86 -DCMAKE_BUILD_TYPE=RELEASE",
                built_command = "-p:Configuration=Release",
            },
            ["win32_debug_x64"] = CxxProjectBuildConfig{ 
                config = "win32_debug_x64",
                cmake = win32_cmake
                    ->replace("$$JE_ENGINE_LIB$$",  "libjoyecs_debug.lib")
                    ->replace("$$JE_WOOLANG_LIB$$", "libwoo_debug.lib")
                    ->replace("$$JE_POSTFIX$$", "_debug"),
                cmake_option = "-G \"Visual Studio 16 2019\" -A x64 -DCMAKE_BUILD_TYPE=DEBUG",
                built_command = "-p:Configuration=Debug",
            },
            ["win32_release_x64"] = CxxProjectBuildConfig{ 
                config = "win32_release_x64",
                cmake = win32_cmake
                    ->replace("$$JE_ENGINE_LIB$$",  "libjoyecs.lib")
                    ->replace("$$JE_WOOLANG_LIB$$", "libwoo.lib")
                    ->replace("$$JE_POSTFIX$$", ""),
                cmake_option = "-G \"Visual Studio 16 2019\" -A x64 -DCMAKE_BUILD_TYPE=RELEASE",
                built_command = "-p:Configuration=Release",
            } 
        };

        return
            config_cmake_file->get(conf)
                ->map(\c: CxxProjectBuildConfig = result::ok(c): result<typeof(c), string>;)
                ->valor(result::err(F"未知的配置{conf}"));
    }

    using compile_context = struct {
        mut display_msg : string,
        mut massive_msg : option<string>,
        mut progress    : int,
        mut show        : bool,
        mut compile_thread  : option<std::thread>,
        mut compile_result  : option<result<result<string, string>, string>>,
    };
    public func update_compile(self: CxxProject, config: string)
    {
        // 起个线程跑编译，然后弄个窗口显示一下进度

        let context = compile_context{
            display_msg = "正在开始编译，请稍等……", 
            progress = 0, 
            show = true,
            compile_thread = option::none,
            compile_result = option::none,
            massive_msg = option::none,
            };
        je::gui::launch(func(){
            context.show = BeginAttrOpen(F"编译C++模块##{JobID()}", WindowsAttribute::ImGuiWindowFlags_None);
                Text(F"正在编译C++模块：{self.name}");
                Text(context.display_msg);
                
                match (context.massive_msg)
                {
                value(msg)?
                {
                    let (w, h) = GetContentRegionAvail();
                    InputTextMultilineSize("##", msg, w, h);
                }
                none? ;
                }

                ProgressBar(context.progress: real / 100.);
            End();
            if (context.show)
                return FormAction::Nothing;
            return FormAction::Close;
        }, ());

        context.compile_thread = 
            option::value(std::thread::create(func ()
            {
                match (self->_compile(config, context))
                {
                ok(_)?
                {
                    // 起个协程同步去更新一下
                    je::gui::launch(
                        func(){
                            let proj = editor::Project::GetCurrentProject();
                            let dump_worlds = proj->DumpAllWorlds();

                            proj->CloseAllWorlds();

                            // 这里的操作非常重要，需要确保全部世界已经被完全正确地关闭，
                            // 否则如果有世界在卸载模块之后才执行清理操作，就有可能发生调用到已经不再有效的析构
                            // 函数。这玩意儿非常恐怖，因此在这里暂时创建一个临时世界，让gui协程能继续运作，然后
                            // 这里yield一下，保证一切按部就班

                            let fakeworld = je::universe::current()->create_world();
                            fakeworld->add_system(proj.graphic_system);

                            std::yield();
                            std::yield();
                            std::yield(); // 玄学，来三下

                            self.module_instance = 
                                self.module_instance
                                    ->map(\m: handle = je::editor::unload_module(m);)
                                    ->bind(\_: anything = option::none;);

                            // 编译成功，更新文件到runtime
                            self->update_library_of_platform(config);

                            // 加载模块！
                            assert(self->load_library_of_platform(config)->has);

                            // 关掉这个临时的世界
                            fakeworld->close();

                            proj->RestoreAllWorlds(dump_worlds);

                            return FormAction::Close;
                        },() );
                }
                err(_)?;
                }
            },()) );
    }
    func _compile(self: CxxProject, config: string, context: compile_context)
    {
        // 输入所需的配置，如果项目支持这个配置，则编译模块
        let current_je_proj = editor::Project::GetCurrentProject();

        let conf = self.configs->get(config);
        if (!conf->has)
        {
            context.show = false;
            return result::err("不支持的配置");
        }

        using je::file;
        using je::filesys;

        context.display_msg = "正在检查工具链";
        context.progress = 0;

        if (!exist(CMAKE_PATH))
        {
            context.display_msg = F"无法找到{CMAKE_PATH}，请检查CMake配置和环境变量";
            return result::err(context.display_msg);
        }
        if (!exist(MSVC_PATH))
        {
            context.display_msg = F"无法找到{MSVC_PATH}，请检查工具链环境变量";
            return result::err(context.display_msg);
        }

        assert(self.path->endwith("/"));

        context.display_msg = "正在扫描并生成loader.hpp";
        context.progress = 5;

        let components = childs(F"{self.path}components")
            ->okay
            ->map(\childs: array<string> = childs
                 ->forall(\f: string = externname(f) == "hpp";)
                 ->map(\f: string = filename(f);)
                 ->map(\f: string = f->subto(0, f->len - 4);)
                ;)
            ->valor([]);
           
        let systems = childs(F"{self.path}systems")
            ->okay
            ->map(\childs: array<string> = childs
                 ->forall(\f: string = externname(f) == "hpp";)
                 ->map(\f: string = filename(f);)
                 ->map(\f: string = f->subto(0, f->len - 4);)
                ;)
            ->valor([]);

        let loader_header = 
@"#pragma once

// Do not modify this file
// 'loader.hpp' will be generated by editor.

// Headers of components/systems
// E.G. #include "ExampleComponent.hpp"
$$INCLUDE_COMPONENT_HEADERS$$
$$INCLUDE_SYSTEM_HEADERS$$

namespace jemodule
{
    void register_types()
    {
        // Here to load components/system
        // e.g. jeecs::typing::type_info::of<Module::ExampleComponent>("Module::ExampleComponent");
$$REGISTER_COMPONENT_TYPES$$
$$REGISTER_SYSTEM_TYPES$$
    }
}
        "@;
        let import_component_headers = 
            components
                ->map(\f: string = F"#include \"{f}.hpp\"";)
                ->reduce(\l: string, r: string = l + "\n" + r;)
                ->valor("");
        let import_system_headers = 
            systems
                ->map(\f: string = F"#include \"{f}.hpp\"";)
                ->reduce(\l: string, r: string = l + "\n" + r;)
                ->valor("");

        let register_component_types =
            components
                ->map(\f: string = F"jeecs::typing::type_info::of<{self.name}::{f}>(\"{self.name}::{f}\");";)
                ->map(\f: string = "        " + f;)
                ->reduce(\l: string, r: string = l + "\n" + r;)
                ->valor("");
        let register_system_types = 
            systems
                ->map(\f: string = F"jeecs::typing::type_info::of<{self.name}::{f}>(\"{self.name}::{f}\");";)
                ->map(\f: string = "        " + f;)
                ->reduce(\l: string, r: string = l + "\n" + r;)
                ->valor("");

        let loader_src = 
            loader_header
                ->replace("$$INCLUDE_COMPONENT_HEADERS$$", import_component_headers)
                ->replace("$$INCLUDE_SYSTEM_HEADERS$$", import_system_headers)
                ->replace("$$REGISTER_COMPONENT_TYPES$$", register_component_types)
                ->replace("$$REGISTER_SYSTEM_TYPES$$", register_system_types);

        if (!writeall(F"{self.path}loader.hpp", loader_src))
        {
            context.display_msg = F"无法写入'{self.path}loader.txt'";
            return result::err(context.display_msg);
        }

        let cmake_command = F"{utils::WrapPath(CMAKE_PATH)} \"{utils::WrapPath(self.path)}\" -B \"{utils::WrapPath(self.path + "msvcbuild_" + config)}\" {conf->val.cmake_option} -DJEMODULE_BUILT_AIM=\"{config}\"";
        
        context.display_msg = F"正在使用CMake生成编译配置\n{cmake_command}";
        context.progress = 15;

        if (open(cmake_command) != 0)
        {
            context.display_msg = "CMake生成失败";
            return result::err(context.display_msg);
        }

        let msvc_proj_path = utils::WrapPath(F"{self.path}msvcbuild_{config}/builder/{config}/{self.name}.vcxproj");
        let msvc_compile_result_path = F"{self.path}msvcbuild_{config}/compile.log";

        delete(msvc_compile_result_path);

        let build_command = F"{utils::WrapPath(MSVC_PATH)} {msvc_proj_path} {conf->val.built_command} -v:quiet -fl -flp:logfile={utils::WrapPath(msvc_compile_result_path)} && echo Finished";

        context.display_msg = F"正在构建二进制文件\n{build_command}";
        context.progress = 45;

        let compile_result_statu_code = open(build_command);
        let compile_result = readall(msvc_compile_result_path);

        context.massive_msg = option::value(compile_result->valor("没有找到编译日志，查看控制台获取更多信息"));

        if (compile_result_statu_code != 0)
        {
            context.display_msg = "C++编译失败";
            return result::err(context.display_msg);
        }
        
        context.display_msg = F"编译完成";
        context.progress = 100;

        current_je_proj.native_modules->getorset(self.path, self);

        return result::ok(context.display_msg);
    }

}