import woo.std;
import je;
import je.gui;
import config;

import builtin.Editor.generic.form.msgbox;
import history;

using std;
using je::gui;
using generic;

namespace editor::Project
{
    enum CreateOrLoadProjectState
    {
        OpenOrCreate,
        OpenProject,
        CreateProject,
    }

    private func _GetAvailableRootPaths()
    {
        // TODO: 这里先只考虑Windows的情况，unix系统那套先不管
        //       而且只考虑A-Z的盘符，什么AA AB AC，就算windows未来支持也给爷滚
        let arr = []: array<char>;
        for (let mut ch = "A"->getch(0); ch <= "Z"->getch(0); ch += 1: char)
            arr->add(ch);

        return arr
            ->trans(\ch: char = [ch]->str + ":/";)
            ->forall(\path: string = je::filesys::isdir(path););
    }

    using Path = struct {
        name: string,       // 仅用于显示
        path: string,
        childs: option<array<Path>>,
    };
    private func _GetRootPaths()
    {
        return _GetAvailableRootPaths()
            ->trans(\rootpath: string = Path{
                name = rootpath,
                path = rootpath,
                childs = option::none};);
    }
    private func _GetChildPaths(path: Path, update: bool)
    {
        if (update || !path.childs->has)
        {
            let arr = 
                je::filesys::childs(path.path)
                    ->unwarp
                    ->forall(\child_path: string = je::filesys::isdir(child_path);)
                    ->trans(\child_path: string = Path{
                        name = je::filesys::filename(child_path),
                        path = child_path,
                        childs = option::none
                    };);

            path.childs = option::value(arr);            
        }

        return path.childs->val;
    }
    private func _ListPath(path: Path, ref selected_path: option<string>)
    {
        let show_child = TreeNodeEx(path.name, 
                TreeNodeAttribute::ImGuiTreeNodeFlags_OpenOnArrow 
                + TreeNodeAttribute::ImGuiTreeNodeFlags_OpenOnDoubleClick 
                + TreeNodeAttribute::ImGuiTreeNodeFlags_SpanAvailWidth
                + selected_path
                    ->map(\p: string = p == path.path;, \ = false;)
                    ->\b: bool = 
                        b   ? TreeNodeAttribute::ImGuiTreeNodeFlags_Selected
                            | TreeNodeAttribute::ImGuiTreeNodeFlags_None;);

        if (IsItemClicked() && !IsItemToggledOpen())
            selected_path = option::value(path.path);

        if (show_child)
        {
            for (let child_path : path->_GetChildPaths(false))
                child_path->_ListPath(ref selected_path);

            TreePop();
        }
    }
    private func _ListAndSelectPathToCreateProject()=> option<string>
    {
        static let mut selected_path = option::none: option<string>;

        static let roots = _GetRootPaths();
        for(let root_path : roots)
            root_path->_ListPath(ref selected_path);

        selected_path = selected_path
            ->bind(\p: string = je::filesys::isdir(p) ? option::value(p) | option::none;);

        return selected_path;
    }

    func _AskCreateOrLoadProject<FT, ArgTs>(form: FT, args: ArgTs)
    {
        // 反正这个窗口就创建一次，所以就搞个静态变量保存状态，也不用还原了
        static let mut state = CreateOrLoadProjectState::OpenOrCreate;
        
        let mut close_form = false;
        Begin("加载或新建项目", 
            WindowsAttribute::ImGuiWindowFlags_AlwaysAutoResize);
            let mut cur_width = 0.;
            GetContentRegionAvail(ref cur_width, 0./* 不关心高度 */);

            if (state == CreateOrLoadProjectState::OpenOrCreate)
            {
                Text("欢迎使用JoyEngine4.0，让我们先从这里开始：");

                if (Button("加载已有的项目", cur_width, 0.))
                    state = CreateOrLoadProjectState::OpenProject;
                if (Button("新建一个项目", cur_width, 0.))
                    state = CreateOrLoadProjectState::CreateProject;
            }
            else if (state == CreateOrLoadProjectState::OpenProject)
            {
                Text("请选择项目文件所在的目录：");

                BeginChild("SelectPathToCreateProject", 0., 300.);
                    let select_path_to_create = _ListAndSelectPathToCreateProject();
                EndChild();

                if (Button("确认", cur_width, 0.))
                {
                    match (select_path_to_create)
                    {
                    none?
                        launch(MsgBox, ("打开项目失败", "请选择项目文件所在的目录"));
                    value(create_proj_in)?
                        {
                            let projname = je::filesys::filename(create_proj_in);
                            let proj_data = 
                                je::file::readall(F"{create_proj_in}/{projname}.jeproj4")
                                    ->bind(\json: string = json->tomap;);

                            match (proj_data)
                            {
                            value(data)?
                            {
                                match(LoadProject(data))
                                {
                                ok(proj)?
                                {
                                    // 更新一下项目历史
                                    update_project_history(proj.path->val);
                                    
                                    // 关闭窗口
                                    close_form = true;
                                }
                                err(msg)?
                                    launch(MsgBox, ("创建项目失败", msg));
                                }
                            }
                            none?
                                launch(MsgBox, ("打开项目失败", F"无法读取项目文件：'{create_proj_in}/{projname}.jeproj4'"));
                            }

                        }
                    }// end of match select_path_to_create
                }
                if (Button("取消", cur_width, 0.))
                    state = CreateOrLoadProjectState::OpenOrCreate;
            }
            else if (state == CreateOrLoadProjectState::CreateProject)
            {
                Text("请输入新建项目名称：");

                static let mut project_name = "";
                InputText("项目名称", ref project_name);
                
                BeginChild("SelectPathToCreateProject", 0., 300.);
                    let select_path_to_create = _ListAndSelectPathToCreateProject();
                EndChild();

                if (Button("开始项目！", cur_width, 0.))
                {
                    if (project_name->trim == "")
                        // 不允许项目名为空
                        launch(MsgBox, ("创建项目失败", "项目名不可以为空，请输入项目名"));
                    else
                    {
                        match (select_path_to_create)
                        {
                        none?
                            launch(MsgBox, ("创建项目失败", "请选择创建项目的路径"));
                        value(create_proj_in)?
                            {
                                // 移除默认的项目，创建新的项目上下文
                                match(CreateProject(project_name->trim, F"{create_proj_in}/{project_name->trim}", true))
                                {
                                ok(proj)?
                                {
                                    // 在这个项目里创建一个默认的世界，并且渲染这个世界
                                    let default_world = proj->CreateWorld().instance;
                                    default_world->editor::name("Root");
                                    default_world->add_system(proj.graphic_system);
                                    default_world->add_system(proj.editor_system);
                                    default_world->add_system(je::typeinfo("Translation::TranslationUpdatingSystem")->val);

                                    // 更新一下项目历史
                                    update_project_history(proj.path->val);

                                    // 关闭窗口，打开主菜单开始编辑
                                    close_form = true;
                                }
                                err(msg)?
                                    launch(MsgBox, ("创建项目失败", msg));
                                } // end of match CreateProject
                            }
                        }// end of match select_path_to_create
                    }
                }
                if (Button("取消", cur_width, 0.))
                    state = CreateOrLoadProjectState::OpenOrCreate;
            }
        End();

        if (close_form)
        {
            launch(form, args);
            return FormAction::Close;
        }
        return FormAction::Nothing;
    }

    func Init()
    {
        // 必须在编辑器初始化时运行，负责创建默认项目环境

        // 0. 创建项目上下文
        let proj = GetCurrentProject();

        // 1. 创建引擎的基本上下文
        let universe = je::universe::editor::create();
        universe->editor::set_current_universe();

        // 2. 创建默认的世界，用于支持启动编辑器的一些gui活动
        let default_world = proj->CreateWorld().instance;
        default_world->editor::name("Default");
        default_world->add_system(proj.graphic_system);
    }

    public func Start<FT, ArgTs>(form: FT, args: ArgTs)
        where form(args...) is FormAction;
    {
        Init();

        // 创建默认的运行环境之后，先不要启动主菜单，也不要做任何事情，
        // 先要求打开或新建项目，记得销毁全部默认世界，否则玩个锤子
        je::gui::launch(_AskCreateOrLoadProject:<FT, ArgTs>, (form, args));

        je::universe::current()->editor::wait();
        je::universe::current()->editor::close();
    }
}
