import woo.std;
import je.gui;
import je;

import builtin.Editor.generic.functions;

import builtin.Editor.generic.form.inputbox;
import builtin.Editor.generic.form.askbox;
import builtin.Editor.generic.form.msgbox;
import builtin.Editor.generic.widget.dragfile;

namespace editor::Inspector::Material
{
    union shader_option
    {
        AddShader(je::graphic::shader),
        DelShader,
        DoNothing,
    }
            
    func show_shader_menu()
    {
        using je::gui;

        let mut menu_option = shader_option::DoNothing;
        if (BeginPopupContextItem())
        {
            if (MenuItem("移除"))
                menu_option = shader_option::DelShader;

            EndPopup();
        }

        return menu_option;
    }

    func show_shader(entity: je::entity, shader: je::graphic::shader, index: int, expand: bool)=> shader_option
    {
        using je::gui;

        let attribute = 
              TreeNodeAttribute::ImGuiTreeNodeFlags_OpenOnArrow 
            + TreeNodeAttribute::ImGuiTreeNodeFlags_OpenOnDoubleClick 
            + TreeNodeAttribute::ImGuiTreeNodeFlags_SpanAvailWidth;

        let textures = entity->editor::graphic::get_textures();

        if (expand)
            SetNextItemOpen(true);

        let show_uniforms = TreeNodeEx(utils::ShortString(shader->path(), 25) + "##" + index:string, attribute);
        let mut shader_operation = show_shader_menu();

        match (generic::AcceptFileForCurrentWidget())
        {
            Drag(shader_path)?
            {
                match (je::graphic::shader::load(shader_path))
                {
                value(shader)?
                    shader_operation = shader_option::AddShader(shader);
                none?
                    generic::msgbox("加载着色器失败", F"无法加载文件{shader_path}作为着色器");
                }
            }
            None? /* 没有文件拖入，不干任何事情 */;
        }
        if (IsItemClickedLeft() && !IsItemToggledOpen())
        {
            // TODO: 总得放点什么在这儿
        }
        if (show_uniforms)
        {
            for(let name, uniform_val : shader->get_uniforms())
            {
                if (name->beginwith("JOYENGINE_"))
                    continue; // 引擎的内置shader-uniform变量，跳过显示

                match (uniform_val)
                {
                    integer(n)?
                        InputInt(name, n) ->> \n = shader->set_uniform(name, n);;
                    float(x)?
                        InputFloat(name, x) ->> \x = shader->set_uniform(name, x);;
                    float2((x, y))?
                        InputFloat2(name, x, y) ->> \xy = shader->set_uniform(name, xy);;
                    float3((x, y, z))?
                        InputFloat3(name, x, y, z) ->> \xyz = shader->set_uniform(name, xyz);;
                    float4((x, y, z, w))?
                        InputFloat4(name, x, y, z, w) ->> \xyzw = shader->set_uniform(name, xyzw);;
                    texture(id)?
                    {
                        let texture_path = 
                            textures->get(id)
                                    ->map(\tex = tex->path->valor("<内置>");)
                                    ->valor(F"<没有绑定到通道{id}的纹理>");
                                
                        match (generic::DragFile(name, texture_path))
                        {
                            Drag(path)?
                            {
                                // 拖动文件到这里，加载这个纹理，然后绑定
                                je::graphic::texture::load(path)
                                    ->> \tex = entity->editor::graphic::bind_texture(id, tex);
                                    ->  or(\=generic::msgbox("加载纹理失败", F"无法加载文件{path}作为纹理");)
                                    ;
                            }
                            None?
                                ; // 没任何变化，啥也不干
                        }
                    }
                    others?
                        Text(F"<不受支持的uniform类型> {name}");
                }
            }
            TreePop();
        }
        return shader_operation;
    }
    public func show(cur_entity : option<je::entity>, max_height: real)
    {
        using je::gui;

        static let mut _last_display_entity = option::none: option<je::entity>;

        let _display_entity_changed = 
            _last_display_entity
                ->bind(\e: je::entity = cur_entity->map(\ce: je::entity = e != ce;);)
                ->valor(true);

        _last_display_entity = cur_entity; // 

        BeginChildSize("material_editor", 0., max_height);
            Text("材质编辑器");
            Separator();
            match (cur_entity)
            {
                value(current_entity)?
                {
                    static let ShadersTypeID = je::typeinfo::load_from_name("Renderer::Shaders")->val();
                    match (current_entity->editor::get_component(ShadersTypeID))
                    {
                        value(shaders)?
                        {
                            let mut shaders = current_entity->editor::graphic::get_shaders();

                            let mut index = 0, mut shaders_has_updated = false;
                            for (let _, shader : shaders->dup())
                            {
                                match (show_shader(current_entity, shader, index, _display_entity_changed))
                                {
                                    AddShader(shad)?
                                    {
                                        shaders = shaders->inlay(index, shad);
                                        shaders_has_updated = true;
                                    }
                                    DelShader?
                                    {
                                        shaders = shaders->erase(index);
                                        shaders_has_updated = true;
                                    }
                                    DoNothing? /* 没有动作 */;
                                }
                                index += 1;
                            } // end show shader.

                            TextDisabled("[拖动shader文件到此处添加新的pass]");
                            match (generic::AcceptFileForCurrentWidget())
                            {
                                Drag(shader_path)?
                                {
                                    // TODO: 为啥会有两个地方加载shader，这个地方的代码写得跟鬼一样
                                    //      检查代码实现，上面也有一个加载shader，两边代码一模一样，
                                    //      搞什么鬼
                                    match (je::graphic::shader::load(shader_path))
                                    {
                                    value(shader)?
                                    {
                                        shaders = shaders->append(shader);
                                        shaders_has_updated = true;
                                    }
                                    none?
                                        generic::msgbox("加载着色器失败", F"无法加载文件{shader_path}作为着色器");
                                    }
                                }
                                None? /* 没有文件拖入，不干任何事情 */;
                            }

                            if (shaders_has_updated)
                                current_entity->editor::graphic::set_shaders(shaders);
                        }
                        none?
                            Text("当前实体没有Renderer::Shaders组件");
                    }
                }
                none?
                    Text("<没有选中可编辑的实体>");
            } // end match
        EndChild();
    }
}