namespace editor::tools
{
    let mut _available_tool_scripts = {}: dict<string, string>;
    public func get_available_tool_scripts()
    {
        return _available_tool_scripts->unmapping->>\kv = kv[0];;
    }
    public func run_tool_script(script: string)
    {
        match (_available_tool_scripts->get(script))
        {
        none?
            return result::err("没有找到此脚本");
        value(path)?
            {
                let vmm = std::vm::create();
                if (! vmm->loadfile(path))
                    return result::err(F"运行脚本失败：{vmm->errmsg()}");
                let r = vmm->run();
                do vmm->close();
                return result::ok(r);
            }
        }
    }
    public func update_tool_scripts(project: Project)
    {
        let new_path_results = {}mut: map<string, string>;

        match (filesys::allsubpath(project.m_path)
            =>> \p = filesys::allsubpath(std::exepath())
                ->> \ep = p->connect(ep);
                ;)
        {
        err(errno)?
            jeapi::gui::form::errmsg("失败", F"获取项目中的工具脚本失败({errno})");
        ok(subpaths)?
            {
                for (let _, path : subpaths)
                    if (filesys::extension(path)->lower == ".jewo")
                    {
                        new_path_results->set(filesys::purename(path), path);
                    }
            }
        }

        _available_tool_scripts = 
                    new_path_results as map<string, string>->unsafe::cast:<dict<string, string>>;
    }
}