import je;
import je::editor;

namespace jeapi::varify
{
    public using EngineVersionInfo = struct{
        engine_core_version: string,
        engine_commit_version: string,
        woolang_version: string,
    }
    {
        using je::editor;

        public func current()
        {
            return EngineVersionInfo{
                engine_core_version = build_version(),
                engine_commit_version = build_commit(),
                woolang_version = woolang_version(),
            };
        }
        public func restore(dat: dict<string, string>)
        {
            return dat->get("engine_core_version")
                =>> \ecv = dat->get("engine_commit_version")
                    =>> \eov = dat->get("woolang_version")
                        ->> \wov = EngineVersionInfo{
                                engine_core_version = ecv,
                                engine_commit_version = eov,
                                woolang_version = wov,
                            };
                        ;
                    ;
                ;

        }
        public func serialize(self: EngineVersionInfo)
        {
            return {
                ["engine_core_version"] = self.engine_core_version,
                ["engine_commit_version"] = self.engine_commit_version,
                ["woolang_version"] = self.woolang_version,
            }->serialize->val;
        }
        public func isequal(self: EngineVersionInfo, another: EngineVersionInfo)
        {
            return self.engine_core_version == another.engine_core_version
                && self.engine_commit_version == another.engine_commit_version
                && self.woolang_version == another.woolang_version;
        }
    }
    public func has_bad_shader(e: je::entity)
    {
        static let BadShadersUniform = je::typeinfo::load("Editor::BadShadersUniform")->val();
        return e->get_component(BadShadersUniform)->has;
    }
    public func varify_all_shader_is_valid_in_world(w: je::world)
    {
        // 检查所有世界中所有实体是否有非法的shader？
        // 返回所有shader非法的实体
        return w->editor::get_all_entities
            -> forall(\e = has_bad_shader(e);)
            ;
    }
}