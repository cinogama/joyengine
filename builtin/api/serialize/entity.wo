import woo::std;

import je;
import je::internal;
import je::towoo;

import pkg::worst;
import pkg::iterator;

using je;

namespace je::api::serialize
{
    using EntityState = struct{
        m_name: string,
        m_is_editor: bool,
        m_components: dict<string, dict<string, string>>,
    }
    {
        public enum RestoreMode
        {
            EDITOR,     // Keep all components.
            RUNTIME,    // Ignore Editor::... components.
        }

        public func dump(e: entity)
        {
            let mut is_editor = false;
            let components_data = {}mut: map<string, dict<string, string>>;

            // e->internal::get_entity_uid;
            for (let (ctype, cdata) : e->internal::get_components)
            {
                let component_data = {}mut: map<string, string>;

                if (ctype == Editor::Invisable::type::typeinfo)
                    // Is invisiable
                    is_editor = true;

                // Walk through the members of the component
                for (let (member_name, member_type, member_addr) : cdata->get_members)
                {
                    if (is_none <| towoo::unsafe::dynamic_parser::saving(member_type, member_addr)
                        ->> \saved_member = component_data->set(member_name, saved_member);)
                    {
                        logwarn(f"无法序列化组件 '{ctype->name}' 的成员 '{member_name}'");
                    }
                }

                // Store~
                components_data->set(ctype->name, component_data->unsafe::asdict);
            }

            return EntityState{
                m_name = e->internal::name,
                m_is_editor = is_editor,
                m_components = components_data->unsafe::asdict,
            };
        }

        public func restore(self: EntityState, at_world: world, mode: RestoreMode)
        {
            let typed_components = 
                self.m_components->keys->iter
                    -> iterator::filter_map(
                        func(str)
                        {
                            if (mode == RestoreMode::RUNTIME && str->begin_with("Editor::"))
                                return option::none;

                            let r = typeinfo::load(str);
                            if (r->is_none)
                                logerr(f"找不到组件 '{str}'");
                            
                            return r;
                        })->iterator::collect;

            let created_entity = at_world->add_entity(typed_components);
            for (let ctype : typed_components)
            {
                let cname = ctype->name;
                let cinstance = created_entity->get_component(ctype)->unwrap;

                for (let (mname, _) : ctype->get_members_info)
                {
                    let (mtype, mhandle) = cinstance->get_member(mname)->unwrap;
                    towoo::unsafe::dynamic_parser::restoring();
                }
            }
        }
    }
}